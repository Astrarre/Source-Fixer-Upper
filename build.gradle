plugins {
    id("java-library")
    id("maven-publish")
    id("signing")
}

group = "io.github.astrarre"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()

    maven {
        name = "FabricMC"
        url = uri("https://maven.fabricmc.net/")
    }
}

dependencies {
    implementation("net.fabricmc:mapping-io:0.3.0")
    implementation(files(RepackagedJavacProvider.createJavacJar(buildDir)))

    implementation("org.ow2.asm:asm-util:9.6")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from(zipTree(RepackagedJavacProvider.createJavacJar(buildDir)))
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            if (signing.signatory != null) {
                signing.sign(this)
            }

            pom {
                name.set("Source Fixer Upper")
                packaging = "jar"

                description.set("A fast remapper for Java source code")
                url.set("https://github.com/Astrarre/Source-Fixer-Upper")

                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://spdx.org/licenses/MIT")
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = uri("${buildDir}/repos/releases")
            def snapshotsRepoUrl = uri("${buildDir}/repos/snapshots")
            name = "Project"
            url = version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
